import promise for Promise
import async for async

import event_horizon as evh
import event_horizon.errors for StatusException
import event_horizon.tcp for TCP

fun read(sock)
    return Promise(fun(resolve, reject)
        sock.readStart(fun(data, status)
            sock.readStop()
            if status < 0
                reject(StatusException(status))
            else
                resolve(data)
            end
        end)
    end)
end

fun write(sock, data)
    return Promise(fun(resolve, reject)
        sock.write(data, fun(status)
            if status < 0
                reject(StatusException(status))
            else
                resolve()
            end
        end)
    end)
end

fun connect(sock, addr, port)
    return Promise(fun(resolve, reject)
        sock.connect(addr, port, fun(status)
            if status < 0
                reject(StatusException(status))
            else
                resolve()
            end
        end)
    end)
end

fun close(sock)
    return Promise(fun(resolve, _)
        sock.close(fun()
            resolve()
        end)
    end)
end

@async
fun client()
    var sock = TCP()
    try
        yield connect(sock, '127.0.0.1', 8080)
        var data = yield read(sock)
        print(data)
    ensure
        yield close(sock)
    end
end

@async
fun handleClient(client)
    yield write(client, "Hello, Async!")
end

fun server()
    var server = TCP()
    server.bind('0.0.0.0', 8080)
    server.listen(fun(client, status)
        handleClient(client) 
        server.close()
        client.close()
    end)
end

//server()
client()

evh.loop().run()
