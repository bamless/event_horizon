import io
import sys

static var tests = []

fun test(fn)
    tests.add(fn)
    return fn
end

fun skip(fn)
    tests.remove(fn)
    return null
end

fun run()
    var numTests = #tests

    print("========")
    print("Running {0} tests" % numTests)
    print("========")
    print("")

    var numPassed = 0
    for var idx, fn in tests.enumerate()
        print("[{0}%] Running test {1}()" % (std.int((idx + 1) / numTests * 100), fn.getName()))

        try
            fn()
            numPassed += 1
        except Exception e
            if !(e is ProgramInterrupt)
                io.stderr.writeln("Test {0}() failed with: {1}({2})" % (fn.getName(), type(e).getName(), e.err())) 
                io.stderr.writeln(e.getStacktrace())
                io.stderr.write('\n')
            end
        end
    end

    print("")
    print("Tests done")
    print("Passed:", numPassed)
    print("Failed:", numTests - numPassed)

    sys.exit(0 if numPassed == numTests else 1)
end
